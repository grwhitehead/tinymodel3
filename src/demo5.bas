10 REM RANDOM LINES GRAPHICS DEMO
20 CLS
100 FOR L = 1 TO 20
110 X1 = RND(127)
120 Y1 = RND(47)
130 X2 = RND(127)
140 Y2 = RND(47)
150 GOSUB 500
160 NEXT L
170 END

500 REM LINE DRAWING ROUTINES FOR THE TRS-80
510 REM BY RICHARD WAGNER AND FREDERICK WAGNER
520 REM CREATIVE COMPUTING, JULY 1983, P142
530 REM
590 DX = X2 - X1
600 DY = Y2 - Y1
610 IX = 1
620 IF DX < 0 THEN IX = -1: DX = -DX
630 IY = 1
640 IF DY < 0 THEN IY = -1: DY = -DY
650 REM
660 REM BRANCH TO PROPER PLOTTING ROUTINE:
670 REM
680 IF DY > DX THEN 920
690 REM FIRST ROUTINE IS USED WHEN DISTANCE BETWEEN POINTS
700 REM THE SAME IN ROTH DIRECTIONS, OR GREATER IN THE X
710 REM DIRECTION.
720 REM
730 EI = 2 * DY
740 ED = EI - 2 * DX
750 E = -DX + EI
760 FOR I = 1 TO DX
770 SET(X1,Y1)
780 X1 = X1 + IX
790 IF E<0 THEN 850
800 Y1 = Y1 + IY
810 E = E + ED
820 NEXT I
830 SET(X1,Y1)
840 RETURN
850 E = E + EI
860 NEXT I
870 SET(X1,Y1)
880 RETURN
890 REM
900 REM SECOND ROUTINE IS USED WHEN DIFFERENCE IN THE Y
910 REM DIRECTION IS GREATER.
920 EI = 2 * DX
930 ED = EI - 2 * DY
940 E = -DY + EI
950 FOR I = 1 TO DY
960 SET(X1,Y1)
970 Y1 = Y1 + IY
980 IF E<0 THEN 1040
990 X1 = X1 + IX
1000 E = E + ED
1010 NEXT I
1020 SET(X1,Y1)
1030 RETURN
1040 E = E + EI
1050 NEXT I
1060 RETURN
